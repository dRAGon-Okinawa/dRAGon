plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'ai'
version = '0.0.1'

java {
	sourceCompatibility = '17'
	targetCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.2.5'
	implementation 'dev.langchain4j:langchain4j-open-ai:0.30.0'
	developmentOnly("org.springframework.boot:spring-boot-devtools")
	runtimeOnly 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.register("bootRunDev") {
    group = "application"
    description = "Runs this project as a Spring Boot application with the dev profile"
    doFirst {
        tasks.bootRun.configure {
            systemProperty("spring.profiles.active", "dev")
        }
    }
    finalizedBy("bootRun")
}

// 1. Registers a task that copies built web app into the service static content.
tasks.register('copyWebApp', Copy) {
	dependsOn(':frontend:jar')
	from "$rootDir/frontend/dist"
	into "$buildDir/resources/main/static/."
}

// 2. Make compilation task dependent on web building, so it will always be performed before.
tasks.named('compileJava') {
	dependsOn(':frontend:jar')
}

// 3. Make resources processing task dependent on copying task, so it will always be performed before.
tasks.named('processResources') {
	dependsOn('copyWebApp')
}

tasks.named('test') {
	useJUnitPlatform()
}
